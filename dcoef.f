      FUNCTION DCOEF(D,L,M,ISIGMA)
      IMPLICIT REAL*8(A-H,O,P,R-Z),LOGICAL*1(Q)
C-----------------------------------------------------------------------
C
C     DCOEF...
C
C        THIS FUNCTION RETURNS THE PROPER COMBINATION OF D-COEFFICIENTS
C     GENERATED BY 'GENDC' WHICH CORRESPOND TO THE ROTATION COEFFICIENT
C     IN THE ANALOGOUS EXPRESSION FOR A REAL SPHERICAL HARMONIC
C
C        Y'(L,M) = SUM(ISIGMA) D(L,M,ISIGMA)*Y(L,ISIGMA)
C                     -L -> L
C
C     VARIABLE DEFINITIONS:
C
C        D(*,*)..... D-COEFFICIENTS GENERATED BY 'GENDC' FOR COMPLEX
C                    SPHERICAL HARMONICS.
C        L3MX....... =(LMAX+1)*(LMAX+2)*(LMAX+3)/6, DIMENSION OF D(*,*).
C        LMAX....... MAXIMUM L-VALUE 
C        BETA....... THE ANGLE OF ROTATION ABOUT THE Y-AXIS.
C        ALPHA...... SUBSEQUENT ROTATION ABOUT NEW Z-AXIS.
C
C     COMMON USAGE:
C
C        /PARMS/  USES - IPARM(20)(=LMAX),  IPARM(28)(=L3MX),
C                        APARM(9)(=ALPHA),  IPARM(10)(=BETA),
C
C     ROUTINES CALLED:  NDXD; MOD, IABS, DSQRT, DSIN, DCOS, DABS
C
C
C-----------------------------------------------------------------------
      COMMON /PARMS/ APARM(20),IPARM(50),QPARM(50)
      EQUIVALENCE (IPARM(20),LMAX),   (IPARM(28),L3MX)
      EQUIVALENCE (APARM(9),ALPHA),   (APARM(10),BETA)
      DIMENSION D(L3MX,2)
      DATA ZERO/0.D0/,ONE/1.D0/,TWO/2.D0/
      DATA SIGNIF/1.D-8/
      PARITY(I)=1-2*MOD(IABS(I),2)
      ROOT2=DSQRT(TWO)
      ANGLE=M*ALPHA
      SINMA=DSIN(ANGLE)
      COSMA=DCOS(ANGLE)
	  COSB=DCOS(BETA)
      MM=M
      ISIG=ISIGMA
      SIG=ONE
      IF (ISIGMA.LT.0) SIG=-ONE
      IT1=1
      IT2=2
      SIGN1=ONE
      SIGN2=PARITY(L+M+ISIGMA)
      IF (M.LT.0) GO TO 10
      GO TO 20
   10 M=-M
      ISIGMA=-ISIGMA
   20 IF (ISIGMA.LT.0) GO TO 30
      GO TO 40
   30 ISIGMA=-ISIGMA
      IT1=2
      IT2=1
      SIGN1=SIGN2
      SIGN2=ONE
   40 IF (M.LT.ISIGMA) GO TO 50
      GO TO 60
   50 MTEMP=M
      M=ISIGMA
      ISIGMA=MTEMP
      SIGN1=SIGN1*PARITY(M+ISIGMA)
      SIGN2=SIGN2*PARITY(M+ISIGMA)
   60 NDX=NDXD(L,M,ISIGMA)
      DCOEF=D(NDX,IT1)*SIGN1+D(NDX,IT2)*SIGN2*SIG
   70 IF (MM) 80,90,100
   80 IF (ISIG) 110,120,130
   90 IF (ISIG) 140,150,160
  100 IF (ISIG) 170,180,190
  110 FACTOR=COSMA
      GO TO 200
  120 FACTOR=-SINMA/ROOT2
      GO TO 200
  130 FACTOR=-SINMA
      GO TO 200
  140 FACTOR=ZERO
      GO TO 200
  150 FACTOR=ONE/TWO
      GO TO 200
  160 FACTOR=ONE/ROOT2
      GO TO 200
  170 FACTOR=SINMA
      GO TO 200
  180 FACTOR=COSMA/ROOT2
      GO TO 200
  190 FACTOR=COSMA
  200 DCOEF=DCOEF*FACTOR
      IF (DABS(DCOEF).LT.SIGNIF) DCOEF=ZERO
      M=MM
      ISIGMA=ISIG
      RETURN
      END
