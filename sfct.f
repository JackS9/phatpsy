      SUBROUTINE SFCT(S,NU,MUMXP1,ALPHA)
      IMPLICIT REAL*8(A-H,O-Z)
C-----------------------------------------------------------------------
C
C     SFCT...
C
C        THIS ROUTINE RETURNS AN ARRAY OF S-FUNCTIONS
C
C             S(I) = S(NU,I-1;ALPHA) I=1,...,MUMXP1
C             (MUMXP1 = MAXIMUM VALUE OF MU + 1)
C
C     USED IN THE COMPUTATION OF ONE-CENTER TWO-ELECTRON INTEGRALS
C     BETWEEN STO'S.  THEY ARE GENERATED BY THE ZERO-BASED RECURSION
C
C             S(NU,MU;ALPHA) = MU*S(NU-1,MU-1;ALPHA) + S(NU,0;ALPHA)
C
C             S(NU,0;ALPHA) = FACT(NU)/(ALPHA+1)**NU
C
C             FACT(NU) = (NU-1)-FACTORIAL.
C
C     COMMON USAGE:
C
C        /TABLES/  USES - FACT(*),REALS(1)(=ONE)
C
C     NOTE:  NU DOES NOT HAVE THE SAME MEANING HERE AS IN 'WFCT'.
C
C-----------------------------------------------------------------------
      DIMENSION S(MUMXP1)
      COMMON /TABLES/ REALS(10),FACT(22),FFAC(19),BINOM(91),HALFPI(8),
     X                ZERO,HALF,ROOT(10),ROOTPI,CONST(10),CONVRT(10)
      EQUIVALENCE (REALS(1),ONE)
      ALFP1=ALPHA+ONE
      NU0=NU-MUMXP1
      DO 10 I=1,MUMXP1
      INU=NU0+I
      MU=MUMXP1-I+1
      S(MU)=FACT(INU)/ALFP1**INU
   10 CONTINUE
      MUMX=MUMXP1-1
      DO 20 I=1,MUMX
      INU=MUMXP1-I
      DO 20 J=1,I
      MU=MUMXP1-J+1
      JMU=I-J+1
      S(MU)=JMU*S(MU)+S(INU)
   20 CONTINUE
      RETURN
      END
