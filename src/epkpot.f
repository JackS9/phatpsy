      SUBROUTINE EPKPOT(PKPOT,EVEC,EVAL,STOVL,SCRAT1,SCRAT2,INDEX)
      IMPLICIT REAL*8(A-H,O,P,R-Z),LOGICAL*1(Q)
C-----------------------------------------------------------------------
C
C     EPKPOT...
C
C        THIS ROUTINE COMPUTES AND ADDS THE ENERGY-DEPENDENT PART OF THE
C     PHILLIPS-KLIENMAN PSEUDOPOTENTIAL FROM THE EIGENVALUES AND VECTORS
C     OF THE LAST SCF CYCLE.  THIS INVOLVES A BACK-TRANSFORMATION TO THE
C     STO-BASIS FROM A DIAGONAL ESTIMATE IN THE HF BASIS.
C
C     VARIABLE DEFINITIONS:
C
C        PKPOT(*,1,*)... ENERGY-INDEPENDENT PART OF THE PK-PSEUDO-
C                        POTENTIAL MATRIX.
C        PKPOT(*,2,*)... ENERGY-DEPENDENT PART OF THE PK-PSEUDO-
C                        POTENTIAL MATRIX.
C        EVEC(*,*,*).... EIGENVECTORS FROM THE LAST CYCLE.
C        EVAL(*,*)...... EIGENVALUES FROM THE LAST CYCLE.
C        STOVL(*)....... STO-OVERLAP MATRIX.
C        SCRAT1(*,*).... SCRATCH ARRAY FOR ACCUMULATING PARTIAL SUMS.
C        SCRAT2(*,*).... SCRATCH ARRAY FOR ACCUMULATING PARTIAL SUMS.
C        INDEX(I)....... =I*(I-1)/2, INDEXING ARRAY.
C        MSTO........... NUMBER OF STO'S IN THE BASIS.
C        M2STO.......... =INDEX(MSTO)+MSTO, DIMENSION OF PKPOT, EPOT2.
C        NORB........... NUMBER OF EIGENVECTORS AND ENERGIES.
C        ISPIN.......... =1 FOR RESTRICTED CASE, =2 FOR UNRESTRICTED.
C
C     ROUTINES CALLED:  MAX0, MIN0
C
C     COMMON USAGE:
C
C        /PARMS/  USES - IPARM(5)(=MSTO),    IPARM(6)(=NORB),
C                        IPARM(27)(=M2STO),  IPARM(33)(=ISPIN)
C
C-----------------------------------------------------------------------
      COMMON /PARMS/ APARM(20),IPARM(50),QPARM(50)
      EQUIVALENCE (IPARM(5),MSTO),    (IPARM(6),NORB),
     X            (IPARM(27),M2STO),  (IPARM(33),ISPIN)
      DIMENSION PKPOT(M2STO,4,ISPIN),
     X          EVEC(MSTO,NORB,ISPIN),    EVAL(NORB,ISPIN),
     X          STOVL(M2STO),             SCRAT1(MSTO,MSTO),
     X          SCRAT2(MSTO,MSTO),        INDEX(M2STO)
      DATA ZERO/0.0D0/
      DO 150 ISP=1,ISPIN
      DO 30 ISTO=1,MSTO
      INDXI=INDEX(ISTO)
      DO 20 JORB=1,NORB
      SUMK=ZERO
      DO 10 KSTO=1,MSTO
      IKSTO=INDXI+KSTO
      IF (KSTO.GT.ISTO) IKSTO=INDEX(KSTO)+ISTO
      SUMK=SUMK+EVEC(KSTO,JORB,ISP)*PKPOT(IKSTO,1,ISP)
   10 CONTINUE
      SCRAT1(ISTO,JORB)=SUMK
   20 CONTINUE
   30 CONTINUE
      DO 50 IORB=1,NORB
      SUMK=ZERO
      DO 40 KSTO=1,MSTO
      SUMK=SUMK+EVEC(KSTO,IORB,ISP)*SCRAT1(KSTO,IORB)
   40 CONTINUE
      SCRAT2(IORB,IORB)=EVAL(IORB,ISP)*SUMK
   50 CONTINUE
      DO 80 ISTO=1,MSTO
      DO 70 JSTO=1,ISTO
      SUMK=ZERO
      DO 60 KORB=1,NORB
      SUMK=SUMK+SCRAT2(KORB,KORB)*EVEC(ISTO,KORB,ISP)*EVEC(JSTO,KORB,
     X     ISP)
   60 CONTINUE
      SCRAT1(ISTO,JSTO)=SUMK
   70 CONTINUE
   80 CONTINUE
      DO 110 ISTO=1,MSTO
      INDXI=INDEX(ISTO)
      DO 100 JSTO=1,MSTO
      SUMK=ZERO
      DO 90 KSTO=1,MSTO
      JKMX=MAX0(JSTO,KSTO)
      JKMN=MIN0(JSTO,KSTO)
      IKSTO=INDXI+KSTO
      IF (KSTO.GT.ISTO) IKSTO=INDEX(KSTO)+ISTO
      SUMK=SUMK+STOVL(IKSTO)*SCRAT1(JKMX,JKMN)
   90 CONTINUE
      SCRAT2(ISTO,JSTO)=SUMK
  100 CONTINUE
  110 CONTINUE
      DO 140 ISTO=1,MSTO
      INDXI=INDEX(ISTO)
      DO 130 JSTO=1,ISTO
      INDXJ=INDEX(JSTO)
      IJSTO=INDXI+JSTO
      SUMK=ZERO
      DO 120 KSTO=1,MSTO
      JKSTO=INDXJ+KSTO
      IF (KSTO.GT.JSTO) JKSTO=INDEX(KSTO)+JSTO
      SUMK=SUMK+SCRAT2(ISTO,KSTO)*STOVL(JKSTO)
  120 CONTINUE
      PKPOT(IJSTO,2,ISP)=PKPOT(IJSTO,2,ISP)+SUMK
  130 CONTINUE
  140 CONTINUE
  150 CONTINUE
      RETURN
      END
