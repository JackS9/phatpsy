      SUBROUTINE PKEPOT(PKPOT,STFOCK,ONTRAN,SCRAT1,SCRAT2,MSTO,M2STO)
      IMPLICIT REAL*8(A-H,O-Z)
C-----------------------------------------------------------------------
C
C     PKEPOT...
C
C        THIS ROUTINE MODIFIES THE FOCK MATRIX TO INCLUDE THE ENERGY-
C     DEPENDENT PART OF THE PSEUDOPOTENTIAL.  THE POTENTIAL IS
C     EXPANDED THROUGH SECOND-ORDER IN OVERLAP AND THEN HERMITIAN
C     AVERAGED.
C
C              -1      -1
C           (PS  F + FS  P) /2
C
C     VARIABLE DEFINITIONS:
C
C        PKPOT(*)...... PROJECTION PART OF THE PSEUDOPOTENTIAL.
C        STFOCK(*)..... FOCK MATRIX (IN STO BASIS).
C        ONTRAN(*,*)... ORTHONORMAL TRANSFORMATION MATRIX ON INPUT.
C                       ALTERED ON OUTPUT.
C        SCRAT1(*,*)... SCRATCH ARRAY.
C        SCRAT2(*,*)... SCRATCH ARRAY.
C        MSTO.......... DIMENSION OF BASIS.
C        M2STO......... =MSTO*(MSTO+1)/2.
C
C     ROUTINES CALLED:  TRISQ(UTHU)
C
C-----------------------------------------------------------------------
      DIMENSION PKPOT(M2STO),STFOCK(M2STO),ONTRAN(MSTO,MSTO),
     X          SCRAT1(MSTO,MSTO),SCRAT2(MSTO,MSTO)
      DATA ZERO/0.0D0/,TWO/2.0D0/
      CALL TRISQ(MSTO,STFOCK,ONTRAN,SCRAT1)
      CALL TRISQ(MSTO,PKPOT,ONTRAN,SCRAT2)
      IJ=0
      DO 30 I=1,MSTO
      DO 20 J=1,I
      IJ=IJ+1
      SUM=ZERO
      DO 10 K=1,MSTO
      SUM=SUM+SCRAT1(I,K)*SCRAT2(J,K)+SCRAT2(I,K)*SCRAT1(J,K)
   10 CONTINUE
      STFOCK(IJ)=STFOCK(IJ)+SUM/TWO
   20 CONTINUE
   30 CONTINUE
      RETURN
      END
