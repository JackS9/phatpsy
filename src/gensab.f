      SUBROUTINE GENSAB(OVRLAP,NA,LA,ETAA,ANORM,NSTOA,NB,LB,ETAB,BNORM,
     X                  NSTOB,RAB,A,B,OMEGAI,OMEGAJ,SCRPLM,INDEX,YLMNRM)
      IMPLICIT REAL*8(A-H,O,P,R-Z),LOGICAL*1(Q)
C-----------------------------------------------------------------------
C
C     GENSAB...
C
C        THIS ROUTINE GENERATES ALL THE TWO-CENTER OVERLAP INTEGRALS
C     BETWEEN STO'S SEPARATED BY RAB (>0) ALONG A COMMON Z-AXIS WITH
C     PARALLEL X- AND Y-AXES (DIATOMIC).
C
C        OVRLAP(IML,IA,JB) = <NAI,LAI,ETAAI!NBJ,LBJ,ETABJ>
C                                                         ML
C
C        !NAI,LAI,ETAAI,ML> = ANORMI*RA**(NAI-1)*EXP(-ETAAI*RA)
C                             *YLMNRM(ILM)*Y(LAI,ML)
C        NAI = NA(I)
C        LAI = LA(I)
C        ETAAI = ETAA(I)
C        ILM = INDEX(LAI+1)+!ML!+1
C        -LAI LE ML LE LAI
C
C     THEY ARE COMPUTED BY TRANSFORMING THE STO'S TO ELLIPTICAL
C     COORDINATES AND EXPANDING IN TERMS OF A- AND B-FUNCTIONS.
C     (SEE 'OGEN', 'ANMBNM', AND 'ABSUM').
C
C     OTHER DEFINITIONS:
C
C        NSTOA.......... NUMBER OF STO'S ON CENTER A (NOT INCLUDING
C                        DIFFERENT ML-VALUES).
C        NSTOB.......... SAME AS NSTOA BUT FOR CENTER B.
C        OMEGAI(*,*).... AN ARRAY FOR HOLDING THE EXPANSION COEFFICIENTS
C                        OF AN STO ON CENTER A IN ELLIPTICAL COORDINATES
C                        (SEE 'OGEN').
C        OMEGAJ(*,*).... SAME AS OMEGAI BUT FOR AN STO ON CENTER B.
C        NNMXM1......... =2*NMAX-1, WHERE NMAX IS THE MAXIMUM VALUE OF
C                        N ENCOUNTERED.  USED AS THE DIMENSIONS OF
C                        OMEGAI(*,*), OMEGAJ(*,*), AND INDEX(*).
C        A(*)........... AN ARRAY FOR HOLDING THE A-FUNCTIONS USED IN
C                        THE EXPANSION OF AN STO (SEE 'ANMBNM').
C        B(*)........... AN ARRAY FOR HOLDING THE B-FUNCTIONS USED IN
C                        THE EXPANSION OF AN STO (SEE 'ANMBNM').
C        NABDIM......... =4*NMAX-1, DIMENSION OF A(*) AND B(*).
C        SCRPLM(*)...... A SCRATCH ARRAY USED BY 'OGEN' FOR HOLDING
C                        VALUES OF THE P(L,M)'S.
C        LMAX........... MAXIMUM L-VALUE ENCOUNTERED.
C        LMXP1.......... =LMAX+1, DIMENSION OF SCRPLM(*) (ALTHOUGH IT
C                        ONLY NEED BE LMAX/2+1).
C        LLMXP1......... =2*LMAX+1, DIMENSION OF OVRLAP(*).
C        NYLM........... =LLMXP1*(LLMXP1+1)/2, DIMENSION OF YLMNRM(*).
C        INDEX(N)....... N*(N-1)/2, INDEXING ARRAY FOR SYMMETRY PACKING.
C        FACT(N+1)...... N-FACTORIAL.
C        BINOM(INDEX(N)+M)... BINOMIAL COEFFICIENT OF X**(N-1)*Y**(M-1).
C        EXPMIN......... MINIMUM EXPONENT ALLOWED TO AVOID UNDERFLOW.
C
C     ROUTINES CALLED:  OGEN, ANMBNM, ABSUM; IABS, DEXP, DABS, MIN0
C
C     COMMON USAGE:
C
C        /PARMS/  USES - IPARM(19)(=NNMXM1),IPARM(21)(=LMXP1),
C                        IPARM(22)(=LLMXP1),IPARM(30)(=NABDIM),
C                        IPARM(34)(=NYLM)
C                        APARM(1)(=EXPMIN)
C
C        /TABLES/ USES - FACT(*),BINOM(*),ZERO,HALF,HALFPI(4)(=TWOPI)
C
C-----------------------------------------------------------------------
      COMMON /PARMS/ APARM(20),IPARM(50),QPARM(50)
      EQUIVALENCE (APARM(1),EXPMIN)
      EQUIVALENCE (IPARM(19),NNMXM1),(IPARM(21),LMXP1),
     X            (IPARM(22),LLMXP1),(IPARM(30),NABDIM),
     X            (IPARM(34),NYLM)
      COMMON /TABLES/ REALS(10),FACT(22),FFAC(19),BINOM(91),HALFPI(8),
     X                ZERO,HALF,ROOT(10),ROOTPI,CONST(10),CONVRT(10)
      EQUIVALENCE (HALFPI(4),TWOPI)
      DIMENSION OVRLAP(LLMXP1,NSTOA,NSTOB), NA(NSTOA),LA(NSTOA),
     X          ETAA(NSTOA),ANORM(NSTOA),   NB(NSTOB),LB(NSTOB),
     X          ETAB(NSTOB),BNORM(NSTOB),   A(NABDIM),B(NABDIM),
     X          OMEGAI(NNMXM1,NNMXM1),      OMEGAJ(NNMXM1,NNMXM1),
     X          SCRPLM(LMXP1),INDEX(NNMXM1),YLMNRM(NYLM)
	 
      RHALF=HALF*RAB
      CNORM=TWOPI*RHALF*RHALF*RHALF
	  
      DO 30 I=1,NSTOA
      ETAAI=ETAA(I)
      ANORMI=ANORM(I)
      RETAAI=RHALF*ETAAI
	  
      NAI=NA(I)
      LAI=LA(I)
      L2AI=INDEX(LAI+1)
	  
      DO 20 J=1,NSTOB
      ETABJ=ETAB(J)
      BNORMJ=BNORM(J)
      RETABJ=RHALF*ETABJ
      RETAI=RETAAI+RETABJ
      RETAJ=RETAAI-RETABJ
      EXPON=DABS(RETAJ)-RETAI
      DEXPON=DEXP(EXPON)
      SNORM=ANORMI*BNORMJ*CNORM*DEXPON
	  
      NBJ=NB(J)
      LBJ=LB(J)
      L2BJ=INDEX(LBJ+1)
	  
      LMIN=MIN0(LAI,LBJ)
      LLMNP1=2*LMIN+1
	  
      DO 10 IML=1,LLMNP1
      OVRLAP(IML,I,J)=ZERO
	  
      IF (EXPON.LE.EXPMIN) GOTO 10
	  
      ML=IML-LMIN-1
      IABSML=IABS(ML)
      MLP1=IABSML+1
	  
      LIMI=NAI-IABSML
      LIMJ=NBJ-IABSML
      LIMIJ=LIMI+LIMJ
	  
      CALL OGEN( NAI,LAI,ML,RHALF,OMEGAI,NNMXM1,BINOM,FACT,INDEX,SCRPLM)
      CALL OGEN(-NBJ,LBJ,ML,RHALF,OMEGAJ,NNMXM1,BINOM,FACT,INDEX,SCRPLM)
      CALL ANMBNM(A,RETAI,B,RETAJ,LIMIJ,IABSML,NABDIM)
      CALL ABSUM(SUM,A,B,OMEGAI,OMEGAJ,LIMI,LIMJ,0)
	  
      IF (ML.NE.0) SUM=HALF*SUM
	  
      SUM=SUM*YLMNRM(L2AI+MLP1)*YLMNRM(L2BJ+MLP1)
	  
      OVRLAP(IML,I,J)=SNORM*SUM
	  
   10 CONTINUE
   
   20 CONTINUE
   30 CONTINUE
   
      RETURN
      END
