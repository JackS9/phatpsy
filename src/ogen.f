      SUBROUTINE OGEN (N,L,ML,RHALF,OMEGA,NODIM,BINOM,GAMMA,KP,PLM)
C...........VERSION = 10/24/73/00
C.......................................................................
C     THIS SUBROUTINE COMPUTES THE COEFFICIENTS, OMEGA(P,Q), OF POWERS
C OF XI AND ETA IN THE EXPANSION OF A SLATER-TYPE ORBITAL IN ELLIPTICAL
C COORDINATES.
C
C     R**(N-1) P(L,ML,COS THETA) = (RHO/RHALF)**IABS(ML)*
C                                  SUM OMEGA(P,Q) XI**(P-1) ETA**(Q-1)
C                                  P,Q
C
C WHERE
C
C     RHO/RHALF = SQRT((XI**2 - 1)*(1 - ETA**2))
C
C     RHALF = RAB/2
C
C AND P AND Q RUN FROM 1 TO N - IABS(ML).  THE ARGUMENTS ARE
C
C     N,L,ML.......STO PARAMETERS.  N MUST BE NEGATIVE FOR AN STO ON
C                  CENTER B.
C     RHALF........RAB/2.
C     OMEGA(*,*)...COEFFICIENT MATRIX (RETURNED).
C     NODIM........ROW/COLUMN DIMENSION OF OMEGA(*,*).
C     BINOM(*).....BINOMIAL COEFFICIENT ARRAY.  BINOMIAL(K,L) MUST BE
C                  AT BINOM(KP(K+1)+L+1).  THE HIGHEST COEFFICIENT
C                  REQUIRED IS BINOMIAL(N-IABS(ML)-1,N-IABS(ML)-1).
C     GAMMA(*).....FACTORIAL ARRAY.  GAMMA(K+1) = FACTORIAL(K).
C     KP(*)........PACKED MATRIX INDEXING ARRAY.  KP(K) = (K*(K-1))/2.
C     PLM(*).......SCRATCH SPACE FOR DEVELOPING LEGENDRE POLYNOMIAL
C                  COEFFICIENTS.  MUST HAVE DIMENSION AT LEAST
C                  AS LARGE AS (L-IABS(ML))/2 + 1.
C
C THE DIMENSION STATEMENT SHOULD BE EQUIVALENT TO
C
C     DIMENSION OMEGA(NODIM,NODIM),BINOM((N-MLABS)*(N-MLABS+1))/2),
C    X          GAMMA(2*L+1),KP(N),PLM((L-MLABS)/2+1)
C
C WHERE MLABS = IABS(ML).
C
C AUTHOR%  NELSON H.F. BEEBE, QUANTUM THEORY PROJECT, UNIVERSITY OF
C          FLORIDA, GAINESVILLE, FLORIDA, AND DIVISION OF THEORETICAL
C          CHEMISTRY, DEPARTMENT OF CHEMISTRY, AARHUS UNIVERSITY,
C          AARHUS, DENMARK
C
C.......................................................................
      IMPLICIT REAL*8 (A-H,O-Z)
      LOGICAL FLIP,ODD
      INTEGER P,Q
      DIMENSION OMEGA(NODIM,NODIM),BINOM(1),GAMMA(1),KP(1),PLM(1)
      ODD(IARG) = (MOD(IARG,2) .NE. 0)
      DATA ZERO/0.D+00/, TWO/2.D+00/
      MLABS = IABS(ML)
      NABS = IABS(N)
C.......................................................................
C ZERO THE ENTIRE COEFFICIENT MATRIX BEFORE STARTING.
C.......................................................................
      DO 20 Q = 1,NODIM
      DO 10 P = 1,NODIM
   10 OMEGA(P,Q) = ZERO
   20 CONTINUE
C.......................................................................
C COMPUTE THE LEGENDRE POLYNOMIAL EXPANSION COEFFICIENTS.  THESE ARE
C STORED IN THE ARRAY PLM(*) AND ARE DEFINED AS FOLLOWS%
C
C     P(L,IABS(ML),COS(THETA)) = RHO**(IABS(ML))*
C
C     SUM PLM(LAMBDA) R**(2*LAMBDA-L) Z**(L-IABS(ML)-2*LAMBDA)
C    LAMBDA
C
C WHERE LAMBDA RUNS FROM 0 TO THE LARGEST INTEGER WHICH DOES NOT EXCEED
C (L - IABS(ML))/2, AND RHO IS DEFINED ABOVE.  SINCE R AND Z IN TERMS
C OF XI AND ETA ARE
C
C     R = RHALF*(XI + SIGMA*ETA)
C
C     Z = RHALF*(XI*ETA + SIGMA)
C
C WHERE SIGMA IS +1 FOR CENTER A AND -1 FOR CENTER B, THE BINOMIAL
C EXPANSION FOR THE POWERS OF R AND Z MAY THEN BE APPLIED TO OBTAIN
C THE EXPLICIT POLYNOMIAL OF XI AND ETA FOR THE INPUT STO.
C.......................................................................
      LM = L - MLABS
      LIMIT = 1 + LM/2
      DO 30 LAMP1 = 1,LIMIT
      LAMBDA = LAMP1 - 1
      LL = KP(L+1) + LAMP1
      TERM = BINOM(LL)*GAMMA(2*(L-LAMBDA)+1)/(GAMMA(LM-2*LAMBDA+1)*
     X     GAMMA(L+1)*TWO**L)
      IF (ODD(MLABS+LAMBDA)) TERM = -TERM
   30 PLM(LAMP1) = TERM
C.......................................................................
C NOW FILL IN THE OMEGA(*,*) MATRIX USING THE EXPLICIT FORMULAE FOR
C THE COEFFICIENTS.  WE USE THE SYMMETRY RELATIONS TO FILL IN THE
C UPPER TRIANGLE FROM THE LOWER TRIANGLE, BUT IGNORE THE ADDITIONAL
C REFLECTION SYMMETRY ABOUT THE SECONDARY DIAGONAL.
C.......................................................................
      RNM1 = RHALF**(NABS-1)
      MAXQ = NABS - MLABS
      DO 60 Q = 1,MAXQ
      FLIP = ((N .LT. 0) .AND. ODD(LM - Q - 1))
      DO 50 P = 1,Q
      SUM = ZERO
      DO 40 LAMP1 = 1,LIMIT
      LAMBDA = LAMP1 - 1
      NLL = NABS + 2*LAMBDA - L - 1
      INDEX1 = NLL + Q - P
      IF (ODD(INDEX1)) GO TO 40
      INDEX1 = INDEX1/2
      IF (INDEX1 .GT. NLL) GO TO 40
      INDEX1 = KP(NLL+1) + INDEX1 + 1
      INDEX2 = P + Q - NLL - 2
      IF (INDEX2 .LT. 0) GO TO 40
      IF (ODD(INDEX2)) GO TO 40
      INDEX2 = INDEX2/2 + 1
      INDEX3 = LM - LAMBDA - LAMBDA + 1
      IF (INDEX2 .GT. INDEX3) GO TO 40
      INDEX2 = KP(INDEX3) + INDEX2
      SUM = SUM + PLM(LAMP1)*BINOM(INDEX1)*BINOM(INDEX2)
   40 CONTINUE
      SUM = RNM1*SUM
      IF (FLIP) SUM = -SUM
      OMEGA(P,Q) = SUM
      IF (P .EQ. Q) GO TO 50
      IF ((N .LT. 0) .AND. ODD(Q - P)) SUM = -SUM
      OMEGA(Q,P) = SUM
   50 CONTINUE
   60 CONTINUE
      RETURN
      END
