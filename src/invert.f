      SUBROUTINE INVERT (MATRIX,INVERS,NSIZE,IERROR,NROW,NCOLUM)
C.......................................................................
C INVERT INVERTS A GENERAL NON-SINGULAR MATRIX AND RETURNS THE INVERSE.
C
C
C THE METHOD IS THAT OF SUCCESSIVE INVERSION.
C
C PARAMETER LIST:
C
C     MATRIX(*,*).......MATRIX TO BE INVERTED
C
C     INVERS(*,*).......INVERSE OF MATRIX
C
C     NSIZE.............SIZE OF MATRIX TO BE INVERTED.
C
C     IERROR............ERROR FLAG.  IF INVERSION IS SUCCESSFUL, IT IS
C                       SET TO 0.  IF INVERSION IS NOT SUCCESSFUL, IT IS
C                       SET TO 1, AND CONROL RETURNS WITH THE INVERSION
C                       INCOMPLETE.
C
C     NROW..............NUMBER OF ROWS SPECIFIED IN THE DIMENSION
C                       STATEMENT FOR MATRIX(*,*) AND INVERS(*,*) IN THE
C                       CALLING PROGRAM.
C
C     NCOLUM............NUMBER OF COLUMNS IN MATRIX(*,*) AND INVERS(*,*)
C
C REFERENCE: P.O. LOWDIN, "ON THE NON-ORTHOGONALITY PROBLEM", TECHNICAL
C            NOTE 146, QUANTUM THEORY PROJECT, UNIVERSITY OF FLORIDA,
C            GAINESVILLE
C
C AUTHOR:  NELSON H.F. BEEBE, QUANTUM THEORY PROJECT, UNIVERSITY OF
C          FLORIDA, GAINESVILLE
C
C REVISED: DECEMBER 18, 1970
C
C.......................................................................
      REAL*8 MATRIX(NROW,NCOLUM),INVERS(NROW,NCOLUM),D22
      INVERS(1,1) = 1.D0/MATRIX(1,1)
      IERROR = 0
      IF (NSIZE.LE.1) RETURN
      DO 50 KBLOCK = 2,NSIZE
C
C CALCULATE THE (KBLOCK-1) BY 1 2-1 BLOCK AND THE 1 BY (KBLOCK-1) 1-2
C
C BLOCK, OMITTING THE FACTOR 1.D0/D22.
C
      KLESS1 = KBLOCK-1
      DO 10 I = 1,KLESS1
      INVERS(I,KBLOCK) = 0.D0
      INVERS(KBLOCK,I) = 0.D0
C
C THE FOLLOWING CODE IS EQUIVALENT TO, BUT SLOWER THAN, THE USE OF DPROD
C
      DO 1 J = 1,KLESS1
      INVERS(KBLOCK,I) = INVERS(KBLOCK,I) + MATRIX(KBLOCK,J)*INVERS(J,I)
    1 INVERS(I,KBLOCK) = INVERS(I,KBLOCK) + INVERS(I,J)*MATRIX(J,KBLOCK)
C
C      INVERS(KBLOCK,I) = DPROD(MATRIX(KBLOCK,1),NROW,INVERS(1,I),1,
C     X     KLESS1)
C      INVERS(I,KBLOCK) = DPROD(INVERS(I,1),NROW,MATRIX(1,KBLOCK),1,
C     X     KLESS1)
C
   10 CONTINUE
C
C CALCULATE 1.D0/D22, THE 1 BY 1 2-2 BLOCK.
C
C THE FOLLOWING CODE IS EQUIVALENT TO, BUT SLOWER THAN, THE USE OF DPROD
C
      D22 = MATRIX(KBLOCK,KBLOCK)
      DO 2 I = 1,KLESS1
    2 D22 = D22 - MATRIX(KBLOCK,I)*INVERS(I,KBLOCK)
C
C      D22 = MATRIX(KBLOCK,KBLOCK) - DPROD(MATRIX(KBLOCK,1),NROW,
C     X     INVERS(1,KBLOCK),1,KLESS1)
C
      IF (D22.NE.0.D0) GO TO 20
      IERROR = 1
      RETURN
   20 D22 = 1.D0/D22
      INVERS(KBLOCK,KBLOCK) = D22
C
C CALCULATE THE KBLOCK BY KBLOCK 1-1 BLOCK.
C
      DO 30 I = 1,KLESS1
      DO 30 J = 1,KLESS1
   30 INVERS(I,J) = INVERS(I,J) + INVERS(I,KBLOCK)*INVERS(KBLOCK,J)*D22
C
C MULTIPLY THE 1-2 AND 2-1 BLOCKS BY -1/D22 TO COMPLETE THE STEP.
C
      DO 40 I = 1,KLESS1
      INVERS(KBLOCK,I) = -INVERS(KBLOCK,I)*D22
   40 INVERS(I,KBLOCK) = -INVERS(I,KBLOCK)*D22
   50 CONTINUE
      RETURN
      END
