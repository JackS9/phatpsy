      SUBROUTINE TWOOUT (MATRIX,NSIZE,INDEX,NCTL)
C...........VERSION = 09/05/73/05
C.......................................................................
C
C TWOOUT PRINTS A REAL*8 SYMMETRIC MATRIX STORED IN ROW-PACKED LOWER
C
C TRIANGULAR FORM (SEE DIAGRAM BELOW) IN FORMATTED FORM WITH ROWS AND
C
C COLUMNS IDENTIFIED.  THE MATRIX IS ASSUMED TO BE ONE FOR A MATRIX
C
C REPRESENTATION OF A TWO-PARTICLE QUANTUM MECHANICAL OPERATOR, SUCH AS
C
C R12 OR 1/R12.  THE ROW AND COLUMN IDENTIFICATIONS GIVE THE FUNCTION
C
C PAIR INDICES IN THE ORDER (1,1), (2,1), (2,2), (3,1), (3,2), (3,3),
C
C ETC.  THE INPUT IS AS FOLLOWS:
C
C        MATRIX(*)......PACKED MATRIX
C
C        NSIZE..........NUMBER OF SINGLE FUNCTIONS FORMING THE PAIR
C                       FUNCTIONS.  THE FULL MATRIX WILL HAVE (NSIZE*
C                       (NSIZE+1))/2 ROWS AND COLUMNS.
C
C        INDEX(*).......INDEXING ARRAY FOR PACKED MATRICES: INDEX(I) =
C                       (I*(I-1))/2.  THERE MUST BE SUFFICIENT ENTRIES
C                       TO COMPUTE INDEX(NROW) WHERE NROW IS THE NUMBER
C                       OF ROWS IN THE PACKED TWO-PARTICLE MATRIX, I.E.
C                       THE NUMBER OF PAIR FUNCTIONS.
C
C        NCTL...........CARRIAGE CONTROL FLAG:  1 FOR SINGLE SPACE,
C                                               2 FOR DOUBLE SPACE, AND
C                                               3 FOR TRIPLE SPACE.
C                       MULLIKEN NOTATION, (I J ! K L ), IS ASSUMED.
C
C THE MATRIX ELEMENTS ARE ARRANGED IN STORAGE AS FOLLOWS:
C
C        1
C        2    3
C        4    5    6
C        7    8    9   10
C       11   12   13   14   15
C       16   17   18   19   20   21
C       22   23   24   25   26   27   28
C
C AND SO ON.
C
C TWOOUT IS SET UP TO HANDLE 6 COLUMNS/PAGE WITH A 6F20.14 FORMAT
C
C FOR THE COLUMNS, AND 50 LINES PER PAGE.  IF DIFFERENT VALUES FOR
C
C THESE ARE REQUIRED, CHANGE FORMATS 1000 AND 2000, INITIALIZE KCOL
C
C TO THE NUMBER OF COLUMNS PER PAGE, AND LINECT TO THE NUMBER OF LINES
C
C PER PAGE.  THE DIMENSIONS ON NK(*) AND NL(*) SHOULD BE THE SAME AS
C
C THE VALUE OF KCOL.
C
C AUTHOR:  NELSON H.F. BEEBE, QUANTUM THEORY PROJECT, UNIVERSITY OF
C          FLORIDA, GAINESVILLE
C
C......................................................................
      REAL*8 MATRIX
      INTEGER NK,NL,INDEX,KCOL,LINECT
	  CHARACTER*1 CTL,ASA(4),LEFT,BAR,RIGHT
      DIMENSION NK(6),NL(6),INDEX(1),MATRIX(1)
      DATA KCOL/6/,LINECT/45/,ZERO/0.D+00/
      DATA ASA/' ','0','-','1'/,LEFT/'('/,BAR/'|'/,RIGHT/')'/
C
C SET UP OUTPUT FORMATS AND PAGE SIZE INDICATORS.
C
      CTL = ASA(1)
      IF ((NCTL.LE.3).AND.(NCTL.GT.0)) CTL = ASA(NCTL)
      INC = MIN0(3,MAX0(0,NCTL))
      LINE = 0
      LSKIP = LINECT - INC
      NROW = INDEX(NSIZE+1)
      LAST = MIN0(NROW,KCOL)
C
C STORE THE PAIR INDICES (KPLACE,LPLACE) IN (NK(*),NL(*)) FOR THE
C COLUMNS CURRENTLY BEING PRINTED.
C
      KLOW = 1
      LLOW = 1
   20 KOUNT = 1
      KHIGH = KLOW + LAST
      DO 40 KPLACE = KLOW,KHIGH
      DO 30 LPLACE = LLOW,KPLACE
      NK(KOUNT) = KPLACE
      NL(KOUNT) = LPLACE
      IF (KOUNT.GE.KCOL) GO TO 50
   30 KOUNT = KOUNT + 1
   40 LLOW = 1
   50 IF (NK(KOUNT) .EQ. NL(KOUNT)) GO TO 60
      LLOW = NL(KOUNT) + 1
      KLOW = NK(KOUNT)
      GO TO 70
   60 LLOW = 1
      KLOW = NK(KOUNT) + 1
C
C WRITE A HEADER FOR THE COLUMNS CURRENTLY BEING PRINTED.
C
   70 WRITE (6,1000) ASA(3),(BAR,NK(I),NL(I),RIGHT,I = 1,LAST)
      LINE = LINE + 3
      NUMCOL = INDEX(NK(1)) + NL(1)
      NCOL = 1
      ILOW = NK(1)
      JLOW = NL(1)
C
C LOOP OVER PAIR INDICES (IPLACE,JPLACE) UNTIL ALL THE ROWS HAVE BEEN
C PRINTED.
C
      DO 90 IPLACE = ILOW,NSIZE
      ISUM = INDEX(IPLACE)
      DO 80 JPLACE = JLOW,IPLACE
      NUMROW = ISUM + JPLACE
      NLAST = INDEX(NUMROW) + NUMCOL - 1
      DO 72 I = 1,NCOL
      IF (MATRIX(NLAST+I) .NE. ZERO) GO TO 74
   72 CONTINUE
      GO TO 76
   74 WRITE (6,2000) CTL,LEFT,IPLACE,JPLACE,(MATRIX(NLAST+I), I = 1,
     &               NCOL)
      LINE = LINE + INC
   76 IF (NCOL .LT. KCOL) NCOL = NCOL + 1
      IF (LINE.LE.LSKIP) GO TO 80
C
C PAGE FULL; REPEAT THE HEADER ON A NEW PAGE AND CONTINUE.
C
      WRITE (6,1000) ASA(4),(BAR,NK(I),NL(I),RIGHT, I = 1,LAST)
      LINE = 0
   80 CONTINUE
   90 JLOW = 1
      NUMCOL = INDEX(NK(LAST)) + NL(LAST)
      LAST = MIN0(KCOL,NROW-NUMCOL)
      IF (NL(LAST) .LT. NSIZE) GO TO 20
 1000 FORMAT (A1,12X,6(5X,A1,I3,1H,,I2,1X,A1,6X))
 2000 FORMAT (2A1,I3,1H,,I2,1H|,1X,6F20.14)
      RETURN
      END
