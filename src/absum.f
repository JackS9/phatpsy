      SUBROUTINE ABSUM(SUM,A,B,OMEGAI,OMEGAJ,LIMI,LIMJ,ITYPE)
      IMPLICIT REAL*8(A-H,O,P,R-Z),LOGICAL*1(Q)
C-----------------------------------------------------------------------
C
C     ABSUM...
C
C        THIS ROUTINE RETURNS THE SUM OF A- AND B-FUNCTIONS USED IN
C     THE COMPUTATION OF ONE-ELECTRON TWO-CENTER INTEGRALS BETWEEN
C     STO'S AND THEIR NORMALIZATION CONSTANTS IN ELLIPTICAL COORDINATES.
C
C        ITYPE = 0 ..... OVERLAP TYPE INTEGRALS.
C              = 1 ..... NUCLEAR ATTRACTION TYPE INTEGRALS.
C        OMEGAI(*,*).... AN ARRAY CONTAINING THE EXPANSION COEFFICIENTS
C                        OF AN STO IN ELLIPTICAL COORDINATES (SEE
C                        'OGEN' FOR DESCRIPTION).
C        OMEGAJ(*,*).... SAME AS OMEGAI(*,*).
C        A(*)........... AN ARRAY FOR HOLDING THE A-FUNCTION VALUES
C                        USED IN THE EXPANSION OF AN STO (SEE 'ANMBNM').
C        B(*)........... SIMILAR TO A(*) BUT FOR THE B-FUNCTIONS.
C        NNMXM1......... =2*NMAX-1, WHERE NMAX IS THE MAXIMUM VALUE OF
C                        N ENCOUNTERED.  USED FOR THE DIMENSION OF
C                        OMEGAI(*,*) AND OMEGAJ(*,*).
C        NABDIM......... =4*NMAX-1, DIMENSION A(*) AND B(*).
C        LIMI........... =N(I)-IABS(ML(I)), UPPER LIMIT ON I-LOOPS.
C        LIMJ........... =N(J)-IABS(ML(J)), UPPER LIMIT ON J-LOOPS.
C
C     COMMON USAGE:
C
C        /PARMS/  USES - IPARM(19)(=NNMXM1),IPARM(30)(=NABDIM)
C
C        /TABLES/ USES - ZERO
C
C-----------------------------------------------------------------------
      COMMON /PARMS/ APARM(20),IPARM(50),QPARM(50)
      EQUIVALENCE (IPARM(19),NNMXM1),(IPARM(30),NABDIM)
      COMMON /TABLES/ REALS(10),FACT(22),FFAC(19),BINOM(91),HALFPI(8),
     X                ZERO,HALF,ROOT(10),ROOTPI,CONST(10),CONVRT(10)
      DIMENSION A(NABDIM),B(NABDIM),OMEGAI(NNMXM1,NNMXM1),
     X          OMEGAJ(NNMXM1,NNMXM1)
      DIMENSION MUNULO(2),MUNUHI(2),MU(3),NU(3)
      DATA MUNULO/1,2/,MUNUHI/1,3/,MU/0,1,0/,NU/0,0,1/
	  
      SUM=ZERO
      MUNUMN=MUNULO(ITYPE+1)
      MUNUMX=MUNUHI(ITYPE+1)
	  
      DO 70 IMUNU=MUNUMN,MUNUMX
      MUM1=MU(IMUNU)-1
      NUM1=NU(IMUNU)-1
	  
      DO 60 IP=1,LIMI
      DO 50 IQ=1,LIMI
      OPQI=OMEGAI(IP,IQ)
      IF (OPQI.EQ.ZERO) GO TO 50
	  
      DO 40 JP=1,LIMJ
      IPJPMU=IP+JP+MUM1
	  
      DO 30 JQ=1,LIMJ
      OPQJ=OMEGAJ(JP,JQ)
      IF (OPQJ.EQ.ZERO) GO TO 30
	  
      IQJQNU=IQ+JQ+NUM1
	  
      IF (ITYPE.EQ.0) GO TO 10	  
      TERM=A(IPJPMU)*B(IQJQNU)
      GO TO 20	  
   10 TERM=A(IPJPMU+2)*B(IQJQNU)-A(IPJPMU)*B(IQJQNU+2)
   
   20 SUM=SUM+TERM*OPQI*OPQJ
   30 CONTINUE
   
   40 CONTINUE
   
   50 CONTINUE
   60 CONTINUE
   
   70 CONTINUE
   
      RETURN
      END
