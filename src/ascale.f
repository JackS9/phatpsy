      SUBROUTINE ASCALE (N,X,A,NADIM)
C...........VERSION = 11/07/73/00
C.......................................................................
C     THIS SUBROUTINE GENERATES THE SCALED A(N,X) FUNCTION DEFINED BY
C
C     A(N,X) = EXP(+X) INTEGRAL (T = 1,INFINITY) T**N EXP(-X*T) DT
C
C N AND X ARE RECEIEVED AS ARGUMENTS, AND ALL THE FUNCTIONS FOR
C INDICES 0,1,...,N ARE RETURNED IN A(1),A(2),...,A(N+1).  EXECUTION
C WILL TERMINATE IF THE SPECIFIED DIMENSION, NADIM, OF A(*) IS
C EXCEEDED.
C     THE FUNCTION IS EVALUATED BY A STABLE RECURSION FORMULA INVOLVING
C SUMS OF POSITIVE TERMS, SO IT SHOULD BE ACCURATE TO THE PRECISION OF
C THE MACHINE BEING USED.
C
C AUTHOR%  NELSON H.F. BEEBE, QUANTUM THEORY PROJECT, UNIVERSITY OF
C          FLORIDA, GAINESVILLE, FLORIDA, AND DIVISION OF THEORETICAL
C          CHEMISTRY, DEPARTMENT OF CHEMISTRY, AARHUS UNIVERSITY,
C          AARHUS, DENMARK
C
C.......................................................................
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION A(NADIM)
      DATA ONE/1.D+00/
      IF (X) 40,40,10
   10 IF (N .GE. NADIM) GO TO 50
      A(1) = ONE/X
      IF (N .EQ. 0) GO TO 30
      NP1 = N + 1
      FIM1 = ONE
      DO 20 I = 2,NP1
      A(I) = A(1) + FIM1*A(I-1)/X
   20 FIM1 = FIM1 + ONE
   30 RETURN
   40 WRITE (6,1000) X
      GO TO 60
   50 WRITE (6,2000) N
   60 STOP
 1000 FORMAT (42H0*** ERROR ***  NON-POSITIVE ARGUMENT, X =,1PE23.15,
     X        19H IN ROUTINE ASCALE.)
 2000 FORMAT (18H0*** ERROR *** N =,I5,47H EXCEEDS SPECIFIED DIMENSION I
     XN ROUTINE ASCALE.)
      END
