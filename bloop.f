      SUBROUTINE BLOOP(NB,LB,ETAB,BNORM,ZPOT2B,CPOT2B,VFIT2B,PKPOTB,
     X                 EVECB,EVALB,OCCB,FOCKB,GAMMAB,STOVLB,STKINB,
     X                 STPOTB,LVB,MVB,VEXPB,VCOEFB,STVAB,
     X                 OVRLAP,STOVLP,OVLAP,BLOCK,
     X                 SCRAT1,SCRAT2,SCRAT3,SCRAT4,
     X                 NA,LA,ETAA,ANORM,ZPOT2A,CPOT2A,STOVLA,
     X                 STPOTA,LVA,MVA,VEXPA,VCOEFA,GAMMAA,
     X                 VFIT2A,PKPOTA,PROJEC,EVALA,OCCA,EVECA,POPUL,
     X                 OVLP,COORD,YLMNRM,
     X                 OMEGAI,OMEGAJ,A,B,D,INDEX,KREC,ZZREPA,RMAXA,
     X                 CGC,QREALY,EVALX,OCCX,EVECX)
      IMPLICIT REAL*8(A-H,O,P,R-Z),LOGICAL*1(Q)
      CHARACTER*2 LATOMB
C-----------------------------------------------------------------------
C
C     BLOOP...
C
C-----------------------------------------------------------------------
      COMMON /PARMS/ APARM(20),IPARM(50),QPARM(50)
      EQUIVALENCE (QPARM(1),QRSTRT),
     X            (QPARM(5),QFIRST),  (QPARM(7),QDEBUG),
     X            (QPARM(8),QSEPAT),  (QPARM(9),QCMPTD),
     X            (QPARM(35),QTRACE)
      EQUIVALENCE (IPARM(1),NATOM),   (IPARM(2),NELEM),
     X            (IPARM(3),NUCZA),   (IPARM(4),IEQATA),
     X            (IPARM(5),MSTOA),
     X            (IPARM(6),NORBA),   (IPARM(7),IORBTA),
     X            (IPARM(8),NORBT),   (IPARM(9),N2ORBT),
     X            (IPARM(10),IATOM),  (IPARM(11),JDFATA),
     X            (IPARM(13),N2ATOM), (IPARM(19),NNMXM1),
     X            (IPARM(20),LMAX),
     X            (IPARM(21),LMXP1),  (IPARM(22),LLMXP1),
     X            (IPARM(24),NCGC),   (IPARM(27),M2STOA),
     X            (IPARM(28),L3MX),   (IPARM(30),NABDIM),
     X            (IPARM(31),NVTRMA),
     X            (IPARM(32),NSTOA),  (IPARM(33),ISPIN),
     X            (IPARM(34),NYLM),   (IPARM(37),MXCYCL),
     X            (IPARM(38),NCYCL),
     X            (IPARM(41),JSTOTA), (IPARM(42),MSTOT)
      EQUIVALENCE (APARM(3),THETA),
     X            (APARM(9),ALPHA),   (APARM(10),BETA)
      COMMON /BPARMS/ IELEMB,IUNITB,MDFATB,JDFAT1,MEQATB,JATOM,
     X                NSTOB,MSTOB,NORBB,N2ORBB,IORBTB,M2STOB,JSTOTB,
     X                NVTRMB
      COMMON /DAUNIT/ NRECS,LENREC,NEXREC,NLRECS,NBYTES,QMXREC
      DIMENSION QFLAGB(8)
      EQUIVALENCE (QFLAGB(1),QFIXDB),  (QFLAGB(2),QVIRTB)
      DIMENSION NA(NSTOA),                LA(NSTOA),
     X          ETAA(NSTOA),              ANORM(NSTOA),
     X          ZPOT2A(M2STOA),           CPOT2A(M2STOA),
     X          LVA(NVTRMA),              MVA(NVTRMA),
     X          VEXPA(NVTRMA),            VFIT2A(NVTRMA),
     X          VCOEFA(NVTRMA),           GAMMAA(NORBA,ISPIN),
     X          STOVLA(M2STOA),           STPOTA(M2STOA,NVTRMA),
     X          PKPOTA(M2STOA,6,ISPIN),   EVECA(MSTOA,NORBA,ISPIN),
     X          EVALA(NORBA,ISPIN),       OCCA(NORBA,ISPIN) 
      DIMENSION NB(NSTOB),
     X          LB(NSTOB),                ETAB(NSTOB),
     X          BNORM(NSTOB),             ZPOT2B(M2STOB),
     X          CPOT2B(M2STOB),           PKPOTB(M2STOB,6,ISPIN),
     X          EVECB(MSTOB,NORBB,ISPIN), EVALB(NORBB,ISPIN),
     X          OCCB(NORBB,ISPIN),        STOVLB(M2STOB),
     X          STKINB(M2STOB),           STPOTB(M2STOB,NVTRMB),
     X          LVB(NVTRMB),              MVB(NVTRMB),
     X          VEXPB(NVTRMB),            VCOEFB(NVTRMB),
     X          VFIT2B(NVTRMB),           FOCKB(N2ORBB,ISPIN),
     X          GAMMAB(NORBB,ISPIN),      POPUL(NORBT,ISPIN),
     X          STVAB(M2STOA,NVTRMB),     OVRLAP(LLMXP1,NSTOA,NSTOB),
     X          STOVLP(MSTOA,MSTOB),      OVLAP(MSTOA,NORBB,ISPIN),
     X          OVLP(N2ORBT,ISPIN),       BLOCK(NORBA,NORBB,ISPIN),
     X          PROJEC(MSTOA,MSTOA,ISPIN),SCRAT1(M2STOA),
     X          SCRAT2(M2STOB),           SCRAT3(MSTOB,MSTOB),
     X          SCRAT4(LMXP1),            COORD(3,NATOM),
     X          EVALX(NORBT,ISPIN),       OCCX(NORBT,ISPIN),
     X          EVECX(MSTOT,NORBT,ISPIN)
      DIMENSION YLMNRM(NYLM),
     X          OMEGAI(NNMXM1,NNMXM1),    OMEGAJ(NNMXM1,NNMXM1),
     X          A(NABDIM),                B(NABDIM),
     X          D(L3MX,2),                KREC(N2ATOM),
     X          INDEX(1),                 PLM(66),
     X          CGC(NCGC)
      COMMON /IODATA/ IUNIT(20),LENBUF
      EQUIVALENCE (IUNIT(1),IUNIT1),(IUNIT(2),IUNIT2),
     X            (IUNIT(3),IUNIT3),(IUNIT(7),IP),
     X            (IUNIT(8),IUDUMP),(IUNIT(10),IUCSF)
      COMMON /TABLES/ REALS(10),FACT(22),FFAC(19),BINOM(91),HALFPI(8),
     X                ZERO,HALF,ROOT(10),ROOTPI,CONST(10),CONVRT(10)
      EQUIVALENCE (REALS(1),ONE), (REALS(2),TWO)
      COMMON /MODPOT/ WGT0,WGT1,WGT2,WGTC,WGTV,VDAMP,VACCEL,
     X                TFEXP(4),TFCOEF(4),LTF(4),MXTF
      COMMON /PUNCHK/ RENRMA,RENRMB,KATOM,KORB,KSP,QPNCHK
      DATA DEGRAD/57.29577951D0/

      FRACTN=ONE-VDAMP
      OCCMX = DFLOAT(3 - ISPIN)

      READ (IUNITB) LATOMB,NUCZB,NB,LB,ETAB,BNORM
      READ (IUNITB) STOVLB,STKINB,STPOTB,LVB,MVB,VEXPB
      REWIND IUNITB
      ZNUCA=NUCZA
      ZNUCB=NUCZB
      JDFAT2=MDFATB
      IF (IELEMB.EQ.NELEM+1) JDFAT2=JDFATA

      DO 250 JDFATB=JDFAT1,JDFAT2
      IF (MEQATB.NE.0) GO TO 240
      READ (IUNIT3) QFLAGB,NEQATB,XB,YB,ZB,EVECB,EVALB,OCCB,FOCKB,
     X     GAMMAB,CPOT2B,VFIT2B,VCOEFB,PKPOTB,ZPOT2B,TOTENB,ZZREPB,RMAXB
      MEQATB=NEQATB
      IF ((IELEMB.EQ.NELEM+1).AND.(JDFATB.EQ.JDFATA)) MEQATB=IEQATA

   10 CONTINUE
      IEQATB=NEQATB-MEQATB
      JATOM=MOD(JATOM,NATOM)+1
      IF (JATOM.EQ.IATOM) GO TO 250
      IJATOM=INDEX(MAX0(IATOM,JATOM))+MIN0(IATOM,JATOM)
      QTRANS=((JATOM.LT.IATOM).AND.(.NOT.QVIRTB))
      CALL RBETA(COORD,NATOM,IATOM,JATOM,RAB,ALPHA,BETA)
      RABMAX=RMAXA+RMAXB
      IF (QFIRST.AND.QSEPAT) GO TO 20
C     IF (RAB.GT.RMAXA) GO TO 20
      IF (RAB.GT.RABMAX) GO TO 20
      ZZREPA=ZZREPA+FRACTN*ZNUCA*ZNUCB/RAB

   20 CONTINUE
      IF (QCMPTD) GO TO 40
      CALL GENDC(D,LLMXP1-1)
      IF (.NOT.QDEBUG) GO TO 30
      ALPHD=DEGRAD*ALPHA
      BETAD=DEGRAD*BETA
      WRITE (IUDUMP,1000) IATOM,JATOM,RAB,ALPHD,BETAD
      WRITE (IUDUMP,2000)
      CALL PUTDC(D,L3MX,LLMXP1-1,IUDUMP)

   30 CONTINUE
      IF (QTRANS) GO TO 40
C     IF ((RAB.GT.RMAXA).AND.(RAB.GT.RMAXB)) GO TO 230
      IF (RAB.GT.RABMAX) GO TO 230
      CALL GENSAB(OVRLAP,NA,LA,ETAA,ANORM,NSTOA,NB,LB,ETAB,BNORM,NSTOB,
     X     RAB,A,B,OMEGAI,OMEGAJ,SCRAT4,INDEX,YLMNRM)
      CALL ROTSAB(STOVLP,OVRLAP,LA,NSTOA,MSTOA,LB,NSTOB,MSTOB,D)
      WRITE (IUNIT1,REC=NEXREC) STOVLP
      KREC(IJATOM)=NEXREC
      INQUIRE (IUNIT1,NEXTREC=NEXREC)
      NBYTES=NBYTES+8*MSTOA*MSTOB
      NLRECS=NLRECS+1
      IF (.NOT.QDEBUG) GO TO 50
      WRITE (IUDUMP,3000) IATOM,JATOM
      CALL PUTMAT(STOVLP,MSTOA,MSTOB,IUDUMP)
      GO TO 50

   40 CONTINUE
C     IF ((RAB.GT.RMAXA).AND.(RAB.GT.RMAXB)) GO TO 230
      IF (RAB.GT.RABMAX) GO TO 230
      JREC=KREC(IJATOM)
      READ (IUNIT1,REC=JREC) STOVLP

   50 CONTINUE
      CALL GENPLM(PLM,BETA,NYLM,LMAX)
      VSUMA=ZERO

      DO 60 NVA=1,NVTRMA
      LV=LVA(NVA)
      IF (LV.LT.0) GO TO 55
      NV=LV
      LV=0
      MV=0
      GO TO 57

   55 NV=-LV+1
      LV=-LV
      MV=MVA(NVA)

   57 CONTINUE
      LMV=INDEX(LV+1)+IABS(MV)+1
      PHI=IABS(MV)*ALPHA
      PHIFAC=DCOS(PHI)
      IF (MV.LT.0) PHIFAC=DSIN(PHI)
      VA=DEXP(-VEXPA(NVA)*RAB)*RAB**(NV-1)*PLM(LMV)*YLMNRM(LMV)
     X   *PHIFAC*TWO*ROOTPI
      VFIT2A(NVA)=VFIT2A(NVA)-FRACTN*ZNUCB*VA
      VSUMA=VSUMA+VCOEFA(NVA)*VA
   60 CONTINUE
   
      CALL RBETA(COORD,NATOM,JATOM,IATOM,RAB,ALPHA,BETA)
      CALL GENPLM(PLM,BETA,NYLM,LMAX)
      VSUMB=ZERO

      DO 65 NVB=1,NVTRMB
      LV=LVB(NVB)
      IF (LV.LT.0) GO TO 62
      NV=LV
      LV=0
      MV=0
      GO TO 63

   62 NV=-LV+1
      LV=-LV
      MV=MVB(NVB)

   63 CONTINUE
      LMV=INDEX(LV+1)+IABS(MV)+1
      PHI=IABS(MV)*ALPHA
      PHIFAC=DCOS(PHI)
      IF (MV.LT.0) PHIFAC=DSIN(PHI)
      VSUMB=VSUMB+VCOEFB(NVB)*DEXP(-VEXPB(NVB)*RAB)*RAB**(NV-1)
     X           *PLM(LMV)*YLMNRM(LMV)*PHIFAC*TWO*ROOTPI
   65 CONTINUE
   
      VABEFF=VSUMA*VSUMB*RAB/(VCOEFA(1)*VCOEFB(1))
      IF (QDEBUG) WRITE(IUDUMP,3500) IATOM,JATOM,VFIT2A
      IA=IORBTA+1
      JB=MOD(IORBTB,NORBT)+1

      DO 90 ISP=1,ISPIN
      IF (QTRANS) GO TO 70
      CALL DMPAB(STOVLP,MSTOA,MSTOB,MSTOA,MSTOB,EVECB(1,1,ISP),MSTOB,
     X     NORBB,MSTOB,NORBB,OVLAP(1,1,ISP),MSTOA,NORBB)
      GO TO 80

   70 CONTINUE
      CALL DMPATB(STOVLP,MSTOB,MSTOA,MSTOB,MSTOA,EVECB(1,1,ISP),MSTOB,
     X     NORBB,MSTOB,NORBB,OVLAP(1,1,ISP),MSTOA,NORBB)

   80 CONTINUE
      CALL DMPATB(EVECA(1,1,ISP),MSTOA,NORBA,MSTOA,NORBA,OVLAP(1,1,ISP),
     X     MSTOA,NORBB,MSTOA,NORBB,BLOCK(1,1,ISP),NORBA,NORBB)
      IF (QTRANS) CALL INSERT(BLOCK(1,1,ISP),
     X     OVLP(1,ISP),IA,JB,NORBA,NORBB)
   90 CONTINUE
   
C     IF (RAB.GT.RMAXA) GO TO 230
      IF (RAB.GT.RABMAX) GO TO 230
      IF (QCMPTD) GO TO 110
      IF (MXCYCL.LE.0) GO TO 230
      CALL GENVAB(STVAB,NA,LA,ETAA,RAB,LVB,MVB,VEXPB,OMEGAI,OMEGAJ,A,B,
     X     SCRAT4,INDEX,ANORM,YLMNRM,D,CGC,QREALY)
      WRITE (IUNIT2) STVAB
      IF (.NOT.QDEBUG) GO TO 120

      DO 100 NV=1,NVTRMB
      WRITE (IUDUMP,4000) IATOM,JATOM,NV
      CALL PUTONE(STVAB(1,NV),MSTOA,IUDUMP)
  100 CONTINUE
  
      GO TO 120

  110 CONTINUE
      READ (IUNIT2) STVAB

  120 CONTINUE
      IF (.NOT.QPNCHK.OR.(KATOM.GT.0).OR.(KORB.EQ.0)) GO TO 160
      IF (IABS(KORB).GT.NORBA) GO TO 160
      IF (QVIRTB) GO TO 160
      JSTOB=0
      MATOMB=IEQATB*MDFATB+JDFATB

      DO 150 ISTOB=1,NSTOB
      LLBP1=2*LB(ISTOB)+1
      DO 140 IMLB=1,LLBP1
      MLB=IMLB-LB(ISTOB)-1
      JSTOB=JSTOB+1
      CIBKA=ZERO
      CIBKB=ZERO

      DO 130 IORBB=1,NORBB
      CIBKA=CIBKA-EVECB(JSTOB,IORBB,KSP)*BLOCK(IABS(KORB),IORBB,KSP)
      IF (JSTOB.EQ.1) RENRMA=RENRMA-BLOCK(IABS(KORB),IORBB,KSP)**2
      CIBKB=CIBKB-EVECB(JSTOB,IORBB,ISPIN)*BLOCK(IABS(KORB),IORBB,ISPIN)
      IF (JSTOB.EQ.1) RENRMB=RENRMB-BLOCK(IABS(KORB),IORBB,ISPIN)**2
  130 CONTINUE
  
      WRITE (IP,5000) NB(ISTOB),LB(ISTOB),MLB,ETAB(ISTOB),BNORM(ISTOB),
     X     LATOMB,MATOMB,XB,YB,ZB,1,2
      WRITE (IP,5100) ONE,ONE
      WRITE (IP,5100) ONE,ONE
      WRITE (IP,5100) CIBKA,CIBKB
  140 CONTINUE
  150 CONTINUE
  
  160 CONTINUE
      IF ((QSEPAT.AND.QFIRST).OR.(FRACTN.EQ.ZERO)) GO TO 230

      DO 180 ISP=1,ISPIN
      IF (.NOT.QTRANS) CALL CSCT(STOVLP,PKPOTB(1,4,ISP),SCRAT3,SCRAT1,
     X                           MSTOB,MSTOA)
      IF (QTRANS) CALL CTSC(STOVLP,PKPOTB(1,4,ISP),SCRAT3,SCRAT1,
     X                      MSTOB,MSTOA)
      CALL SCMULT(SCRAT1,FRACTN,M2STOA)
      CALL ADDMAT(PKPOTA(1,1,ISP),SCRAT1,PKPOTA(1,1,ISP),M2STOA)
      IF (.NOT.QTRANS) CALL CSCT(STOVLP,PKPOTB(1,5,ISP),SCRAT3,SCRAT1,
     X                           MSTOB,MSTOA)
      IF (QTRANS) CALL CTSC(STOVLP,PKPOTB(1,5,ISP),SCRAT3,SCRAT1,
     X                      MSTOB,MSTOA)
      CALL SCMULT(SCRAT1,FRACTN,M2STOA)
      CALL ADDMAT(PKPOTA(1,2,ISP),SCRAT1,PKPOTA(1,2,ISP),M2STOA)
      IF (.NOT.QTRANS) CALL CSCT(STOVLP,PKPOTB(1,6,ISP),SCRAT3,SCRAT1,
     X                           MSTOB,MSTOA)
      IF (QTRANS) CALL CTSC(STOVLP,PKPOTB(1,6,ISP),SCRAT3,SCRAT1,
     X                      MSTOB,MSTOA)
      CALL SCMULT(SCRAT1,FRACTN,M2STOA)
      CALL ADDMAT(PKPOTA(1,3,ISP),SCRAT1,PKPOTA(1,3,ISP),M2STOA)
  180 CONTINUE
  
      DO 220 ISTOA=1,MSTOA
      INDXI=INDEX(ISTOA)
      DO 210 JSTOA=1,ISTOA
      IJSTOA=INDXI+JSTOA
      ZPOTIJ=-ZNUCB*STVAB(IJSTOA,1)
      ZPOT2A(IJSTOA)=ZPOT2A(IJSTOA)+FRACTN*ZPOTIJ
      DO 190 NV=1,NVTRMB
      CPOTIJ=VCOEFB(NV)*STVAB(IJSTOA,NV)
      CPOT2A(IJSTOA)=CPOT2A(IJSTOA)+FRACTN*CPOTIJ
  190 CONTINUE
  210 CONTINUE
  220 CONTINUE

      DO 228 ISP=1,ISPIN  
      DO 226 IORBA=1,NORBA
      IF (JATOM.EQ.MOD(IATOM,NATOM)+1) 
     X   POPUL(IORBTA+IORBA,ISP) = OCCA(IORBA,ISP)
      POPSUM = ZERO
      DO 224 IORBB=1,NORBB
C     IF (QTRACE) WRITE (9,*) '[',IATOM,'-',IORBA,'-',ISP,']',
C    X            '[',JATOM,'-',IORBB,'-',ISP,']' 
C     IF (QTRACE) WRITE (9,*) '  EVALA=',EVALA(IORBA,ISP),' EVALB=',EVALB(IORBB,ISP)
C     IF (QTRACE) WRITE (9,*) '  OCCA=',OCCA(IORBA,ISP),' OCCB=',OCCB(IORBB,ISP)
      EDIFF = EVALB(IORBB,ISP)-EVALA(IORBA,ISP)
      ESUM  = (EVALA(IORBA,ISP)+EVALB(IORBB,ISP))/TWO
      FERMIA = ONE/(ONE+DEXP((EVALA(IORBA,ISP)-ESUM)/THETA))
      FERMIB = ONE/(ONE+DEXP((EVALB(IORBB,ISP)-ESUM)/THETA))
C     IF (QTRACE) WRITE (9,*) '  FERMIA=',FERMIA,' FERMIB=',FERMIB,
C    X            ' SUM=',FERMIA+FERMIB
      CT = (OCCA(IORBA,ISP)+OCCB(IORBB,ISP))*FERMIA
     X     - OCCA(IORBA,ISP)
C     IF (QTRACE) WRITE (9,*) '  CT=',CT
      IF ((CT.GT.ZERO).AND.(CT.GT.OCCB(IORBB,ISP)))
     X   CT = OCCB(IORBB,ISP)
      IF ((CT.GT.ZERO).AND.(CT.GT.(OCCMX-OCCA(IORBA,ISP))))
     X   CT = OCCMX-OCCA(IORBA,ISP)
      IF ((CT.LT.ZERO).AND.(-CT.GT.OCCA(IORBA,ISP)))
     X   CT = -OCCA(IORBA,ISP)
      IF ((CT.LT.ZERO).AND.(-CT.GT.(OCCMX-OCCB(IORBB,ISP))))
     X   CT = -(OCCMX-OCCB(IORBB,ISP))
C     IF (QTRACE) WRITE (9,*) '  ADJ CT=',CT
      CT = CT*BLOCK(IORBA,IORBB,ISP)**2
      POPSUM = POPSUM + CT
C     IF (QTRACE) WRITE (9,*) '  OVLP=',BLOCK(IORBA,IORBB,ISP),'  OVLP*CT=',CT
C     IF (QTRACE) WRITE (9,*) '  NEW OCCA=',POPUL(IORBTA+IORBA,ISP)+POPSUM
  224 CONTINUE
C     IF (POPSUM.LT.ZERO) POPSUM = ZERO
C     IF (POPSUM.GT.OCCMX) POPSUM = OCCMX
      POPUL(IORBTA+IORBA,ISP) = POPUL(IORBTA+IORBA,ISP) + POPSUM
  226 CONTINUE
  228 CONTINUE
  
  230 CONTINUE
      IF (.NOT.QVIRTB) IORBTB=MOD(IORBTB+NORBB-1,NORBT)+1
      IF (MEQATB.EQ.0) GO TO 250

  240 CONTINUE
      READ (IUNIT3) QFLAGB,ISYMB,IXYZB,XB,YB,ZB,EVECB,EVALB,OCCB,FOCKB,
     X     GAMMAB,CPOT2B,VFIT2B,VCOEFB,PKPOTB,ZPOT2B,TOTENB,ZZREPB,RMAXB
      MEQATB=MEQATB-1
      GO TO 10
  250 CONTINUE
  
      JDFAT1=1
      RETURN

 1000 FORMAT(//' R(',I2,'-',I2,') =',F9.4,
     X         ', ALPHA =',F9.4,', BETA =',F9.4)
 2000 FORMAT(//' D-COEFFICIENTS...')
 3000 FORMAT(//' ',I2,'-',I2,' OVERLAP MATRIX...')
 3500 FORMAT(//' ',I2,'-',I2,' NUCLEAR ATTRACTION TERMS...'//
     X       ' ',5F10.5/(' ',5F10.5))
 4000 FORMAT(//' ',I2,'-',I2,' MODEL POTENTIAL MATRIX, TERM',I2,'...')
 5000 FORMAT(/,3I3,2F12.5,1X,A2,I3,3F10.5,I3,I2)
 5100 FORMAT(F10.5,$)
      END
